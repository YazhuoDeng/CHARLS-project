

Title: CHARLS data leisure PA constrained structural model  

Data:
     File is mydataImp1.dat;

Variable:
     Names are
          ID  householdID communityID
          gender marry age vig_index
          mod_index walk_index vigL modL walkL
          vigNL modNL walkNL
          adl1-adl5 dc009-dc018
          urban facility tpa lpa;

     Usevariables are 
          adl1-adl5 dc009-dc012
          dc014 dc015 dc017-dc018
          urban facility lpa;
          
     GROUPING=urban (1=rural 2=urban);
     
Analysis:
     Estimator = ML;
     Bootstrap =20000;


Model:

     dep BY dc009-dc012 dc014 dc015 dc017-dc018;
     adl BY adl1-adl5;
     lpa ON facility ;
     dep ON facility (2); !path equally constrained between groups
     adl ON facility ;
     adl ON lpa ;
     dep ON lpa ;
     dep ON adl (6); !path equally constrained between groups
          adl1  WITH adl2;
 
Model indirect:
       dep IND facility;
  	adl IND facility;
  	dep IND lpa;

          
Model urban:
     
     lpa ON facility ;
     dep ON facility (2); !path equally constrained between groups
     adl ON facility ;
     adl ON lpa ; 
     dep ON lpa ;
     dep ON adl (6); !path equally constrained between groups
          adl1  WITH adl2;
  Output:
       TECH1 TECH4 CINTERVAL(BCBOOTSTRAP) standardized;




